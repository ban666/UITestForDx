*** Settings ***
Resource          ../Lib.txt
Resource          ../DX_Test_Info.txt
Library           ../../public/UserTest.py

*** Variables ***
${weixin_uid}     ozX3swyHfP0KBdTisUzdIw7RbUkY
${weibo_uid}      6051955234
${qq_uid}         3FBE43EF635AFEDEE2BE8551977FE519

*** Test Cases ***
PhoneBindTest
    [Documentation]    本条用例主要验证以下功能：
    ...    1、手机号绑定任一第三方账号能够成功
    [Setup]    ClearAccountSettings
    [Template]    PhoneBindTest
    1
    2
    3

PhoneBindAllTest
    [Documentation]    本条用例主要验证以下功能：
    ...    1、手机号绑定全部第三方账号能够成功
    ...    2、绑定第三方账号后，使用任一账号登录进入的都是同一账号
    ${userinfo}    regist_and_login    13477777777
    ${user_bind}    bind_third_test    ${userinfo['dc']}    1
    ${user_bind}    bind_third_test    ${userinfo['dc']}    2
    ${user_bind}    bind_third_test    ${userinfo['dc']}    3
    ${userinfo_1}    third_login_test    1
    ${userinfo_2}    third_login_test    2
    ${userinfo_3}    third_login_test    3    True
    Should Be Equal As Integers    ${userinfo_1['data']['userinfo']['phone']}    13477777777
    Should Be Equal As Integers    ${userinfo_2['data']['userinfo']['phone']}    13477777777
    Should Be Equal As Integers    ${userinfo_3['data']['userinfo']['phone']}    13477777777

ThirdBindThirdTest
    [Documentation]    本条用例主要验证以下功能：
    ...    1、第三方账号之间能够互相绑定
    [Template]    ThirdBindThirdTest
    1    2    3    ${weibo_uid}    sinaweibouid
    2    1    3    ${qq_uid}    qquid
    3    1    2    ${weixin_uid}    weixinuid

BindExsitThirdTest
    [Documentation]    本条用例主要验证以下功能：
    ...    1、已登陆过的第三方账号，绑定能够提示账号已使用，不会绑定成功
    [Template]    BindExistThirdTest
    1    2
    1    3
    2    1
    2    3
    3    1
    3    2

ThirdBindPhoneTest
    [Documentation]    本条用例主要验证以下功能：
    ...    1、第三方账号能够成功绑定未登录过的手机号
    [Template]    ThirdBindPhoneTest
    1
    2
    3

ThirdBindExsitPhoneTest
    [Documentation]    本条用例主要验证以下功能：
    ...    1、第三方账号绑定已登录过的手机号，能够提示手机号已使用，不会绑定成功
    [Template]    ThirdBindExsitPhoneTest
    1
    2
    3
