*** Settings ***
Resource          ../Lib.txt
Resource          ../DX_Test_Info.txt
Library           ../../public/UserTest.py

*** Test Cases ***
RegisterAndLogin
    [Documentation]    本条用例主要验证以下功能：
    ...    1、手机号可正常注册登录
    ${userinfo}    regist_and_login    13477777777    True
    log    ${userinfo}
    Should Be Equal As Integers    ${userinfo['code']}    0
    Should Be Equal As Integers    ${userinfo['data']['userinfo']['phone']}    13477777777

ErrorVcode
    [Documentation]    本条用例主要验证以下功能：
    ...    1、手机号登录时，输入错误的验证码，不能登录能够正常提示
    ${userinfo}    login_test    1333333333    1234
    log    ${userinfo}
    Should Be Equal As Integers    ${userinfo['code']}    30004

ThirdLogin
    [Documentation]    本条用例主要验证以下功能：
    ...    1、使用不同类型的第三方账号可正确登录
    [Template]    ThirdLogin
    2
    1
    3

AccountSwitch
    [Documentation]    本条用例主要验证以下功能：
    ...    1、切换不同账号时，用户信息不出现紊乱
    ${userinfo_phone}    regist_and_login    13477777777    True
    log    ${userinfo_phone['data']['userinfo']['uid']}
    ${userinfo_third}    third_login_test    2    True
    log    ${userinfo_third['data']['userinfo']['uid']}
    Should Not Be Equal As Integers    ${userinfo_phone['data']['userinfo']['uid']}    ${userinfo_third['data']['userinfo']['uid']}

ThirdNameLengthTest
    [Documentation]    本条用例主要验证以下功能：
    ...    1、当第三方昵称超过12个字时，登陆后自动阶段到12个字
    [Template]    ThirdNameLengthTest
    2    3FBE43EF635AFEDEE2BE8551977FE519    ben123456789789451561651    http://q.qlogo.cn/qqapp/1101963208/3FBE43EF635AFEDEE2BE8551977FE519/100
    3    ozX3sw5huKJtronYi_I-6u5SdlrM    wechat_test1234567890123456    http://fake_icon
    1    6013515625    weibo_test12345674894654165132    http://tva2.sinaimg.cn/default/images/default_avatar_male_180.gif
